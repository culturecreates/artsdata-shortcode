# SPARQL that inserts venues based on events organized by organizers with a location

PREFIX schema: <http://schema.org/>
insert {
    graph <http://kg.artsdata.ca/culture-creates/artsdata-planet-capacoa/capacoa-members> {
        ?org schema:location ?roleUri .
        ?roleUri schema:location ?venue ;
                 a schema:Role ;
                 schema:roleName "organizes events at"  .
        ?venue a ?type ;
               schema:name ?name ; 
               schema:identifier ?id .
    }
}
where {
    select distinct ?org ?venue ?type ?name ?roleUri where {
        values ?org {
            <http://kg.artsdata.ca/resource/K10-440> 
        }
        ?org ^schema:organizer/schema:location ?venue .
        ?venue a ?type ;
               schema:name ?name .
        optional { ?venue schema:identifier ?id }.
        bind(URI(concat(str(?org),"-role-",str(?venue))) as ?roleUri)
    }
}

